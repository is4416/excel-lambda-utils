# ğŸ•’ OverlapTime â€” Excel LAMBDAé–¢æ•°ã§æ™‚åˆ»ã®é‡è¤‡ã‚’è¨ˆç®—

æ™‚åˆ»ã®é‡è¤‡ã‚’è¨ˆç®—ã—ã¾ã™  
æ™‚é–“å¸¯ã«ã‚ˆã£ã¦å˜ä¾¡ãŒç•°ãªã‚‹å¥‘ç´„ã®å®Ÿç¸¾æ™‚é–“ãªã©ã€æ™‚é–“å¸¯åˆ¥é›†è¨ˆã®è¨ˆç®—ã«ä½¿ç”¨ã§ãã¾ã™

**å¼•æ•°**

| å¼•æ•°      | å‹                      | èª¬æ˜                 |
| --------- | ----------------------- | -------------------- |
| StartDate | Number (Excel Datetime) | é–‹å§‹æ—¥æ™‚             |
| EndDate   | Number (Excel Datetime) | çµ‚äº†æ—¥æ™‚             |
| MinTime   | Number (Excel Datetime) | é‡è¤‡é–‹å§‹æ™‚åˆ»         |
| MaxTime   | Number (Excel Datetime) | é‡è¤‡çµ‚äº†æ™‚åˆ»         |
| Result    | Number (Excel Datetime) | é‡è¤‡æ™‚é–“ã®åˆè¨ˆã‚’è¿”ã™ |

**å‚™è€ƒ**

- Resultã¯æˆ»ã‚Šå€¤ã§ã™ã€‚å¼•æ•°ã¨ã—ã¦ã¯ä¸è¦ã§ã™ã€‚
- è¿”ã‚Šå€¤ã¯ã€Œæ—¥å˜ä½ã®æ•°å€¤ã€ã§ã™ã€‚
- æ™‚é–“è¡¨ç¤ºã«ã™ã‚‹å ´åˆã¯ã€ã‚»ãƒ«ã®è¡¨ç¤ºå½¢å¼ã‚’`[h]:mm`ç­‰ã«è¨­å®šã—ã¦ãã ã•ã„ã€‚
- MinTime > MaxTime (22:00-5:00ã®ã‚ˆã†ãªæ—¥ã¾ãŸãç¯„å›²) ã«ã‚‚å¯¾å¿œã—ã¦ã„ã¾ã™ã€‚

**ã‚³ãƒ¼ãƒ‰**

```excel
= LAMBDA(StartDate,EndDate,MinTime,MaxTime, LET(
  StartTime, MOD(StartDate, 1),
  EndTime  , MOD(EndDate, 1),
  MinT     , MOD(MinTime, 1),
  MaxT     , MOD(MaxTime, 1),
  Buf      , INT(EndDate) - INT(StartDate),
  Days     , IF(Buf > 0, Buf, IF(StartTime >= EndTime, 1, 0)),

  TimeOfOneDay, MaxT - MinT + IF(MinT < MaxT, 0, 1),
  TimeOfDays  , (Days - 1) * TimeOfOneDay,

  FirstDayUpperLimit, MaxT,
  FirstDayUpperTime , IF(Days > 0, 1, EndTime),
  FirstDayUpper     , IF(FirstDayUpperLimit < FirstDayUpperTime,
    FirstDayUpperLimit, FirstDayUpperTime
  ),

  FirstDayLowerLimit, IF(MinT >= MaxT, 0, MinT),
  FirstDayLowerTime , StartTime,
  FirstDayLower     , IF(FirstDayLowerLimit < FirstDayLowerTime,
    FirstDayLowerTime, FirstDayLowerLimit
  ),
  TimeOfFirstDay, FirstDayUpper - FirstDayLower,

  TimeOfFirstDayBefore, IF((MinT >= MaxT) * (MaxT < StartTime) = 0, 0,
    IF(Days > 0, 1, EndTime) - IF(StartTime < MinT, MinT, StartTime)
  ),

  LastDayUpper, IF(MaxT < EndTime, MaxT, EndTime),

  LastDayLowerLimit, IF(MinT >= MaxT, 0, MinT),
  LastDayLowerTime , IF(StartTime < EndTime, StartTime, 0),
  LastDayLower     , IF(LastDayLowerLimit < LastDayLowerTime,
    LastDayLowerTime, LastDayLowerLimit
  ),

  TimeOfLastDay, IF(Days > 0, LastDayUpper - LastDayLower, 0),

  IF(TimeOfFirstDay < 0, 0, TimeOfFirstDay) +
  IF(TimeOfFirstDayBefore < 0, 0, TimeOfFirstDayBefore) +
  IF(TimeOfDays < 0, 0, TimeOfDays) +
  IF(TimeOfLastDay < 0, 0, TimeOfLastDay)
))
```

**å¤‰æ•°ã®è©³ç´°**

- StartTime: Number (Excel Datetime), StartDate ã®æ™‚åˆ»éƒ¨åˆ†
- EndTime  : Number (Excel Datetime), EndDate ã®æ™‚åˆ»éƒ¨åˆ†
- MinT     : Number (Excel Datetime), MinTime ã®æ™‚åˆ»éƒ¨åˆ†
- MaxT     : Number (Excel Datetime), MaxTime ã®æ™‚åˆ»éƒ¨åˆ†
- Buf      : é–‹å§‹æ—¥ã¨çµ‚äº†æ—¥ã®å·®
- Days     : çµŒéæ—¥æ•°ã€‚StartTime >= EndTime ã®ã¨ãã¯1æ—¥è£œæ­£

- TimeOfOneDay: 1æ—¥ã‚ãŸã‚Šã®é‡è¤‡æ™‚é–“ã€‚MinT > MaxT ã®ã¨ãã¯1æ—¥è£œæ­£
- TimeOfDays  : ä¸­é–“æ—¥ã®åˆè¨ˆé‡è¤‡æ™‚é–“

- FirstDayUpperLimit: åˆæ—¥ã®ä¸Šé™åŸºæº–
- FirstDayUpperTime : åˆæ—¥ã®çµ‚äº†æ™‚åˆ»
- FirstDayUpper     : é‡è¤‡ã™ã‚‹ä¸Šé™æ™‚é–“ (MIN(FirstDayUpperLimit, FirstDayUpperTime))
- FirstDayLower     : åˆæ—¥ã®ä¸‹é™æ™‚é–“
- TimeOfFirstDay    : åˆæ—¥ã®é‡è¤‡æ™‚é–“

- TimeOfFirstDayBefore: æ—¥ã¾ãŸãæ™‚ã®æ™‚é–“èª¿æ•´

- LastDayLowerLimit: æœ€çµ‚æ—¥ã®ä¸‹é™åŸºæº–
- LastDayLowerTime : æœ€çµ‚æ—¥ã®é–‹å§‹æ™‚åˆ»
- LastDayLower     : é‡è¤‡ã™ã‚‹ä¸‹é™æ™‚é–“ (MAX(LastDayLowerLimit, LastDayLowerTime))
- LastDayUpper     : æœ€çµ‚æ—¥ã®ä¸Šé™æ™‚é–“
- TimeOfLastDay    : æœ€çµ‚æ—¥ã®é‡è¤‡æ™‚é–“

**ä½¿ç”¨ä¾‹**

OverlapTime ã¨ã„ã†åå‰ã§ã€ãƒ–ãƒƒã‚¯ã«ç™»éŒ²ã—ã¦ã„ã‚‹ã‚‚ã®ã¨ã—ã¾ã™
> ã‚¹ãƒ”ãƒ«ã«ã‚‚å¯¾å¿œã—ã¦ã„ã¾ã™

```excel
= OverlapTime(A1:A10, B1:B10, TIMEVALUE("08:30"), TIMEVALUE("17:15"))
```
