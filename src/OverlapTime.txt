= LAMBDA(StartDate, EndDate, MinTime, MaxTime, LET(
  StartTime, MOD(StartDate, 1),
  EndTime  , MOD(EndDate, 1),
  MinT     , MOD(MinTime, 1),
  MaxT     , MOD(MaxTime, 1),
  Buf      , INT(EndDate) - INT(StartDate),
  Days     , IF(Buf > 0, Buf, IF(StartTime >= EndTime, 1, 0)),

  TimeOfOneDay, MaxT - MinT + IF(MinT < MaxT, 0, 1),
  TimeOfDays  , (Days - 1) * TimeOfOneDay,

  FirstDayUpperLimit, IF(Days > 0, 1, MaxT),
  FirstDayUpperTime , IF(Days > 0, 1, EndTime),
  FirstDayUpper     , IF(FirstDayUpperLimit < FirstDayUpperTime,
    FirstDayUpperLimit, FirstDayUpperTime
  ),
  FirstDayLower , IF(StartTime < MinT, MinT, StartTime),
  TimeOfFirstDay, FirstDayUpper - FirstDayLower,

  LastDayLowerLimit, IF(Days > 0, 0, MinT),
  LastDayLowerTime , IF(StartTime < EndTime, StartTime, 0),
  LastDayLower     , IF(LastDayLowerLimit < LastDayLowerTime,
    LastDayLowerTime,
    LastDayLowerLimit
  ),
  LastDayUpper , IF(MaxT < EndTime, MaxT, EndTime),
  TimeOfLastDay, IF(Days > 0, LastDayUpper - LastDayLower, 0),

  IF(TimeOfFirstDay < 0, 0, TimeOfFirstDay) + 
  IF(TimeOfDays < 0, 0, TimeOfDays) + 
  IF(TimeOfLastDay < 0, 0, TimeOfLastDay)
))
