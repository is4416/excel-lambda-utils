' ---------- ---------- ----------
' WEB版 Excel で動作確認済み
' ---------- ---------- ----------

= LAMBDA(S, LET(
  S_Count, LEN(S),
  S_List, SEQUENCE(S_Count),

  QuotList, MAP(
    S_List,
    LAMBDA(i, LET(
      C, MID(S, i, 1),
      PrevC, IF(i = 1, "", MID(S, i - 1, 1)),
      NextC, IF(i = LEN(S), "", MID(S, i + 1, 1)),
      IF(AND(C = """", PrevC <> """", NextC <> """"), i, 0)
    ))
  ),
  CleanQuotList, FILTER(QuotList, QuotList > 0),

  Q_Count, ROWS(CleanQuotList),
  Q_List, SEQUENCE(CEILING(Q_Count / 2, 1)),

  EscapeAreas_Start, IF(Q_Count = 0, Q_List, MAP(
    Q_List,
    LAMBDA(i, INDEX(CleanQuotList, (i - 1) * 2 + 1))
  )),

  EscapeAreas_End, IF(Q_Count = 0, Q_List, MAP(
    Q_List,
    LAMBDA(i, IF(i * 2 > Q_Count, 0, INDEX(CleanQuotList, i * 2)))
  )),

  DelimiterList, MAP(
    S_List,
    LAMBDA(i, IF(i = S_Count, i, LET(
      c, MID(S, i, 1),
      IF(c = ",", i, 0)
    )))
  ),

  DelimiterFlags, MAP(
    DelimiterList,
    LAMBDA(i, IF(i = 0,
      FALSE,
      SUM((EscapeAreas_End > 0) * (EscapeAreas_Start < i) * (EscapeAreas_End > i)) = 0
    ))
  ),
  CleanDelimiterList, FILTER(DelimiterList, DelimiterFlags),

  TextList, TRIM(MAP(
    SEQUENCE(ROWS(CleanDelimiterList)),
    LAMBDA(i, LET(
      Start, IF(i = 1, 1, INDEX(CleanDelimiterList, i - 1) + 1),
      Size, INDEX(CleanDelimiterList, i) - Start + 1,
      Buf, TRIM(MID(S, Start, Size)),
      Item, IF(i = ROWS(CleanDelimiterList), Buf, MID(Buf, 1, LEN(Buf) - 1)),
      L, IF(LEN(Item) > 0, LEFT(Item, 1), ""),
      R, IF(LEN(Item) > 1, RIGHT(Item, 1), ""),
      SUBSTITUTE(
        IF(AND(L = """", R = """"), MID(Item, 2, LEN(Item) - 2), Item),
        """""",
        """"
      )
    ))
  )),

  TextList
))

' ---------- ---------- ----------
' 動作が不安定なので、参考
' ---------- ---------- ----------

= LAMBDA(Text, LET(
  Buf       , TRIM(Text),
  OuterLeft , LEFT(Buf, 1),
  OuterRight, RIGHT(Buf, 1),
  S, IF(OR(
    AND(OuterLeft = "{", OuterRight = "}"),
    AND(OuterLeft = "[", OuterRight = "]")
  ), MID(Buf, 2, LEN(Buf) - 2), Buf),
  NumberList, SEQUENCE(LEN(S)),
  Result    , REDUCE(
    { {}, "", False },
    NumberList,
    LAMBDA(acc, i, LET(
      List   , INDEX(acc, 1),
      Buf    , INDEX(acc, 2),
      Flag   , INDEX(acc, 3),
      C      , MID(S, i, 1),
      Esc    , IF(i > 1, MID(S, i - 1, 1) = "\", False),
      NewFlag, IF(AND(C = """", NOT(Esc)), NOT(Flag), Flag),
      NewList, IF(OR(AND(C = ",", NewFlag = False), i = LEN(S)),
        VSTACK(List, Buf),
        List
      ),
      NewBuf , IF(OR(C = ",", AND(C = """", NOT(NewFlag))),
        "",
        IF(Esc,
          LEFT(Buf, MAX(0, LEN(Buf) - 1)) & C,
          Buf & C
        )
      ),
      { NewList, NewBuf, NewFlag }
    ))
  ),
  INDEX(Result, 1)
))
